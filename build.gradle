plugins {
    id 'fabric-loom' version '1.1-SNAPSHOT'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release.set(17)
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}

publishing {

    publications {
        gpr(MavenPublication) {

            artifactId = "disable-custom-worlds-advice"

            pom {
                name = "Disable Custom Worlds Advice"
                description = "Minecraft asking you if you want to load a custom world is annoying. Let's fix it!"
                url = "https://www.curseforge.com/minecraft/mc-mods/fabric-disable-custom-worlds-advice"
                licenses {
                    license {
                        name = "GNU General Public License, Version 3"
                        url = "https://www.gnu.org/licenses/gpl-3.0.html"
                    }
                }
                developers {
                    developer {
                        id = "rdvdev2"
                        name = "Roger Díaz Viñolas"
                        email = "rdvdev2@gmail.com"
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/rdvdev2/DisableCustomWorldsAdvice.git"
                    developerConnection = "scm:git:https://github.com/rdvdev2/DisableCustomWorldsAdvice.git"
                    url = "https://github.com/rdvdev2/DisableCustomWorldsAdvice"
                }
            }

            from components.java
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/rdvdev2/DisableCustomWorldsAdvice")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
}